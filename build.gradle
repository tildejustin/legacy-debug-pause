plugins {
    id "fabric-loom" version "$loom_version"
    id "legacy-looming" version "$legacy_looming_version"
    id "maven-publish"
}

version "$mod_version+$target_version"
group maven_group

base {
    archivesName = archives_name
}

legacyLooming {
    intermediaryVersion = 2
}

repositories {
    maven { url "https://jitpack.io" }
    flatDir { dirs "libs" }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings(legacy.yarn(minecraft_version, yarn_build))
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    include(implementation(annotationProcessor "io.github.llamalad7:mixinextras-fabric:0.2.1-beta.2"))
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding "UTF-8"
    it.options.release.set(8)
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

jar {
    from("LICENSE")
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
